apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: "io.gitlab.arturbosch.detekt"
apply plugin: 'com.github.dcendents.android-maven'

group = 'com.github.Xlopec'
version = '0.0.1-alpha1'

buildscript {
    ext.kotlin_version = '1.3.50'
    ext.detect_version = '1.0.0-RC16'
    ext.coroutines_version = '1.3.0'

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.18"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detect_version"
    }
}

android {
    compileSdkVersion 29


    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        // will disable warning about usage of experimental Kotlin features
        freeCompilerArgs = [
                '-Xuse-experimental=kotlin.ExperimentalUnsignedTypes',
                '-Xuse-experimental=kotlin.Experimental',
                '-Xuse-experimental=kotlinx.coroutines.FlowPreview',
                '-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi',
                '-XXLanguage:+NewInference'
        ]
    }
}

project.afterEvaluate {
    tasks.findByName("preReleaseBuild").dependsOn("detekt")
}

detekt {
    toolVersion = detect_version
    input = files("src/main/kotlin", "src/main/java")
    filters = ".*/resources/.*,.*/build/.*"
    baseline = file("detekt-baseline.xml")
    config = files("$projectDir/detekt-config.yml")
}

task formatCode(type: io.gitlab.arturbosch.detekt.Detekt) {

    parallel = true
    autoCorrect = true
    disableDefaultRuleSets = true
    buildUponDefaultConfig = true
    failFast = false
    ignoreFailures = false
    setSource(files(projectDir))

    include("**/*.kt")
    include("**/*.kts")
    exclude("**/resources/**")
    exclude("**/build/**")

    config = files("$projectDir/detekt-config.yml")
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaAndroidTask) {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"

    externalDocumentationLink {
        url = new URL("https://developer.android.com/reference/")
        url = new URL("https://kotlin.github.io/kotlinx.coroutines/kotlinx-coroutines-core/")
    }
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from "$buildDir/javadoc"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
    exclude "**/Kotlin*"
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

androidExtensions {
    experimental = true
}

junitJacoco {
    jacocoVersion = '0.8.4'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    testImplementation "io.strikt:strikt-core:0.22.2"
    testImplementation 'org.robolectric:robolectric:4.3'

    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
}
