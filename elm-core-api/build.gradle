/*
 * Copyright (C) 2019 Maksym Oliinyk.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'org.jetbrains.dokka'
apply plugin: "io.gitlab.arturbosch.detekt"
apply plugin: 'com.github.dcendents.android-maven'

group 'com.github.Xlopec'
version '0.0.2-alpha1'

buildscript {

    ext {
        detect_version = '1.0.0-RC16'
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.18"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detect_version"
    }
}

repositories {
    mavenLocal()
    jcenter()
}

mainClassName = "com.oliynick.max.elm.core.component.RunnerKt"

project.afterEvaluate {
    //tasks.findByName("jar").dependsOn("detekt")
}

detekt {
    toolVersion = detect_version
    input = files("src/main/kotlin", "src/main/java")
    filters = ".*/resources/.*,.*/build/.*"
    baseline = file("detekt-baseline.xml")
    config = files("$projectDir/detekt-config.yml")
}

task formatCode(type: io.gitlab.arturbosch.detekt.Detekt) {

    parallel = true
    autoCorrect = true
    disableDefaultRuleSets = true
    buildUponDefaultConfig = true
    failFast = false
    ignoreFailures = false
    setSource(files(projectDir))

    include("**/*.kt")
    include("**/*.kts")
    exclude("**/resources/**")
    exclude("**/build/**")

    config = files("$projectDir/detekt-config.yml")
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaAndroidTask) {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"

    externalDocumentationLink {
        url = new URL("https://kotlin.github.io/kotlinx.coroutines/kotlinx-coroutines-core/")
    }
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from "$buildDir/javadoc"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.java.srcDirs
    exclude "**/Kotlin*"
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

junitJacoco {
    jacocoVersion = '0.8.4'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation "io.ktor:ktor-client-websockets:$ktor_version"
    implementation "io.ktor:ktor-client-okhttp:$ktor_version"

    testImplementation project(path: ':elm-core-test', configuration: 'default')
    testImplementation project(path: ':elm-core-actor', configuration: 'default')

    testImplementation 'org.robolectric:robolectric:4.3'

    api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
}
